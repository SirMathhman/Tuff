// Fluff: the Tuff linter.
//
// Fluff runs parse+analyze across the module graph and reports warnings/errors.
// Configuration is loaded from build.json (auto-discovered upward).

extern from rt::stdlib use { println, stringLen, stringCharCodeAt, readTextFile };
extern from rt::vec use { vec_len, vec_get };

from tuffc_lib use { fluff_project_with_reader };
from analyzer use { set_fluff_options, set_fluff_complexity_options, set_fluff_file_size_options };
from build_config use { load_fluff_config };
from util::diagnostics use { set_diagnostics_format, has_project_errors, reset_project_errors };

fn print_usage() : Void => {
  println("usage: fluff [options] <in.tuff>");
  println("options:");
  println("  --format <human|json>          Diagnostics output format");
  println("config:");
  println("  build.json (auto-discovered upward from <in.tuff>)");
}

fn main(argv: Vec<String>) => {
  let mut format = "human";
  let mut inPath = "";

  let mut i = 0;
  while (i < vec_len(argv)) {
    let a = vec_get(argv, i);

    if (a == "--format") {
      if (i + 1 >= vec_len(argv)) {
        print_usage();
        yield 1;
      }
      format = vec_get(argv, i + 1);
      i = i + 2;
      continue;
    }

    if (stringLen(a) > 0 && stringCharCodeAt(a, 0) == 45) {
      println("unknown option: " + a);
      print_usage();
      yield 1;
    }

    if (inPath == "") { inPath = a; i = i + 1; continue; }

    println("too many arguments");
    print_usage();
    yield 1;
  }

  if (inPath == "") {
    print_usage();
    yield 1;
  }

  set_diagnostics_format(format);
  reset_project_errors();

  let cfg = load_fluff_config(inPath);
  set_fluff_options(cfg.unusedLocals, cfg.unusedParams);
  set_fluff_complexity_options(cfg.complexity, cfg.complexityThreshold);
  set_fluff_file_size_options(cfg.maxFileLines, cfg.maxFileLinesThreshold);

  fluff_project_with_reader(inPath, readTextFile);
  if (has_project_errors()) { yield 1; }
  0
}
