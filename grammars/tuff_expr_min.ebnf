// Minimal expression grammar used to incrementally validate generated parsers.
// This is intentionally smaller than full tuff.ebnf.

expr ::= if_expr | add_expr ;

if_expr ::= "if" "(" expr ")" expr "else" expr ;

add_expr ::= mul_expr (("+" | "-") mul_expr)* ;
mul_expr ::= unary_expr (("*" | "/") unary_expr)* ;

unary_expr ::= ("-" unary_expr) | ("!" unary_expr) | postfix_expr ;

postfix_expr ::= atom postfix* ;

postfix ::= call | field | index ;
call ::= "(" arg_list? ")" ;
arg_list ::= expr ("," expr)* ;
field ::= "." ident ;
index ::= "[" expr "]" ;

atom ::= int_literal | ident | "(" expr ")" ;

int_literal ::= [0-9]+ ;
ident ::= [a-zA-Z_][a-zA-Z0-9_]* ;
